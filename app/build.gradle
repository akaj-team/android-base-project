apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "com.android.base"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    applicationVariants.all { variant ->
        switch (variant.getName()) {
            case "debug":
                variant.buildConfigField "String", "BASE_API_URL", '"https://api.github.com/"'
                break
            case "release":
                variant.buildConfigField "String", "BASE_API_URL", '""'
                break
        }
    }

    lintOptions {
        abortOnError false
        lintConfig file('lint.xml')
        xmlOutput file("${buildDir}/reports/lint-results.xml")
    }

    testOptions {
        unitTests.all {
            setIgnoreFailures(true)
            include "**/*Test.class"
            exclude "**/element/*Test.class"
        }
        unitTests.returnDefaultValues = true
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'

    // Test
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-inline:2.21.0'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'com.squareup.okhttp3:mockwebserver:3.9.1'
    testImplementation "org.robolectric:robolectric:3.6.1"

    // ConstraintLayout
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // RecycleView
    implementation "androidx.recyclerview:recyclerview:1.1.0"

    // Retrofit 2
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.1.0'

    // Glide
    implementation("com.github.bumptech.glide:glide:4.11.0") {
        exclude group: "com.android.support"
    }
    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    // Rxjava - RxAndroid
    implementation 'io.reactivex.rxjava2:rxjava:2.2.10'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
}

apply from: "ci-kotlin.gradle"
apply from: "jacoco.gradle"
